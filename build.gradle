
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.palantir:jacoco-coverage:0.4.0'
    }
}

plugins {
    id 'com.gradle.build-scan' version '3.6.2'
}

description = "Mocca is a GraphQL client for JVM languages with the goal of being easy to use, flexible and modular."

apply plugin: 'com.palantir.jacoco-full-report'

subprojects {

    group = 'com.paypal.mocca'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    apply plugin: 'maven'
    apply plugin: 'java-library'
    apply plugin: 'signing'

    def javaVersion = JavaVersion.VERSION_1_8
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }

    compileJava {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
    
    jar {
        manifest {
            attributes 'Implementation-Version': version,
                    'Implementation-Name': name,
                    'Implementation-Vendor': 'PayPal'
        }
    }

    test.useTestNG()

    pmd {
        ignoreFailures = true
        pmdTest.enabled= false
        pmdMain.enabled = true
    }

    tasks.withType(Pmd){
        reports{
            xml.enabled=true
            html.enabled=true
            xml.destination file("${buildDir}/reports/pmd/pmd_report.xml")
            html.destination file("${buildDir}/reports/pmd/pmd_report.html")
        }
    }

    jacoco {
        toolVersion = "0.8.4"
    }

    jacocoTestCoverageVerification {
        dependsOn test

        violationRules {
            rule { element = 'PACKAGE'; limit { minimum = 0.50 }; includes = ['com.paypal.mocca.client.*'] }
        }
    }

    jacocoTestReport {
        dependsOn jacocoTestCoverageVerification
        
        def coverageProjects = subprojects
        reports {
            xml.enabled true
            html.enabled true
            csv.enabled false
            xml.destination file("${buildDir}/jacoco/jaccoco_report.xml")
            html.destination file("${buildDir}/jacoco/html")
        }
    }

    build.finalizedBy(jacocoTestReport)

}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    def coverageProjects = subprojects.stream().filter({sb -> !(sb.name in [
            // Projects without Java code
            // none

            // API projects without concrete classes, or relevant classes, to be tested
            // none

            // Test projects
            'mocca-functional-tests',
    ])}).collect()

    additionalSourceDirs = files(coverageProjects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(coverageProjects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(coverageProjects.sourceSets.main.output)
    executionData = files(coverageProjects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false

        html.destination file("${buildDir}/reports/jacoco/html")
        xml.destination file("${buildDir}/reports/jacoco/coverage.xml")
    }
}

// Dependencies version management
ext.lib = [

        // Java specs
        slf4j_api: 'org.slf4j:slf4j-api:1.7.25',
        javax_servlet: 'javax.servlet:javax.servlet-api:4.0.1',
        jaxrs2: 'javax.ws.rs:javax.ws.rs-api:2.1.1',

        // Open Feign
        feign_core: 'io.github.openfeign:feign-core:10.11',
        feign_apache: 'io.github.openfeign:feign-httpclient:10.11',
        feign_hc5: 'io.github.openfeign:feign-hc5:10.11',
        feign_google: 'io.github.openfeign:feign-googlehttpclient:10.11',
        feign_java11: 'io.github.openfeign:feign-java11:10.11',
        feign_jaxrs2: 'io.github.openfeign:feign-jaxrs2:10.11',
        feign_okhttp: 'io.github.openfeign:feign-okhttp:10.11',
        feign_micrometer: 'io.github.openfeign:feign-micrometer:10.11',

        // Observability
        micrometer: 'io.micrometer:micrometer-core:1.3.5',

        // HTTP clients (make sure these versions are aligned with the versions used by Feign)
        apache_client: 'org.apache.httpcomponents:httpclient:4.5.10',
        hc5_client: 'org.apache.httpcomponents.client5:httpclient5:5.0-beta7',
        google_client: 'com.google.http-client:google-http-client:1.31.0',
        okhttp_client: 'com.squareup.okhttp3:okhttp:4.6.0',

        // Jersey
        jersey_client: "org.glassfish.jersey.core:jersey-client:2.32",
        jersey_hk2: "org.glassfish.jersey.inject:jersey-hk2:2.32",
        jersey_media_json_jackson: 'org.glassfish.jersey.media:jersey-media-json-jackson:2.32',
        jersey_netty_connector: "org.glassfish.jersey.connectors:jersey-netty-connector:2.32",
        jersey_test: "org.glassfish.jersey.test-framework:jersey-test-framework-core:2.32",
        jersey_jetty: "org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-jetty:2.32",
        jackson_provider: "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.10.3",

        // Graphql
        graphql_tools: "com.graphql-java-kickstart:graphql-java-tools:6.1.0",
        
        // Jetty
        jetty_server: "org.eclipse.jetty:jetty-server:9.2.28.v20190418",

        // Utils
        activation: 'javax.activation:activation:1.1.1',
        commmons: 'commons-io:commons-io:2.6',
        logback: 'ch.qos.logback:logback-classic:1.2.3',

        // Jackson
        jackson_annotations: 'com.fasterxml.jackson.core:jackson-annotations:2.9.9',
        jackson_databind: 'com.fasterxml.jackson.core:jackson-databind:2.9.9',
        jackson_datatype_jsr301: 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.9',

        // Resilience
        resilience4j_feign: 'io.github.resilience4j:resilience4j-feign:1.3.0',
        resilience4j_retry: 'io.github.resilience4j:resilience4j-retry:1.3.0',
        resilience4j_circuitbreaker: 'io.github.resilience4j:resilience4j-circuitbreaker:1.3.0',
        resilience4j_ratelimiter: 'io.github.resilience4j:resilience4j-ratelimiter:1.3.0',
        resilience4j_bulkhead: 'io.github.resilience4j:resilience4j-bulkhead:1.3.0',

        // Tests
        testng: 'org.testng:testng:6.14.3',
        slf4j_simple: 'org.slf4j:slf4j-simple:1.7.25',
        wiremock: 'com.github.tomakehurst:wiremock:2.24.1',
        mockito_core: 'org.mockito:mockito-core:3.3.0'
]
